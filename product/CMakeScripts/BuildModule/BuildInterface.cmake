include_guard()
include (LinkTargetIncludeDirectories)
include (BuildInstallModule)
function(BuildInterface)
    message(STATUS "====Start Build Interface====")
    set(options)
    set(oneValueArg MODULE_NAME IDE_FOLDER)
    set(multiValueArgs 
                        TARGET_SOURCE_HEADER_BASE_DIR TARGET_SOURCE_PUBLIC_HEADER
                        TARGET_INCLUDE_DIRECTORIES_BUILD_INTERFACE TARGET_INCLUDE_DIRECTORIES_INSTALL_INTERFACE
                        TARGET_DEPENDENCIES_PUBLIC)
    cmake_parse_arguments(INTERFACE "${options}" "${oneValueArg}" "${multiValueArgs}" ${ARGN})

    message(STATUS "Parse Args Results:")
    message(STATUS "MODULE_NAME: ${INTERFACE_MODULE_NAME}")
    message(STATUS "TARGET_SOURCE_HEADER_BASE_DIR: ${INTERFACE_TARGET_SOURCE_HEADER_BASE_DIR}")
    message(STATUS "TARGET_SOURCE_PUBLIC_HEADER: ${INTERFACE_TARGET_SOURCE_PUBLIC_HEADER}")
    message(STATUS "TARGET_INCLUDE_DIRECTORIES_BUILD_INTERFACE: ${INTERFACE_TARGET_INCLUDE_DIRECTORIES_BUILD_INTERFACE}")
    message(STATUS "TARGET_INCLUDE_DIRECTORIES_INSTALL_INTERFACE: ${INTERFACE_TARGET_INCLUDE_DIRECTORIES_INSTALL_INTERFACE}")
    message(STATUS "TARGET_DEPENDENCIES_PUBLIC: ${INTERFACE_TARGET_DEPENDENCIES_PUBLIC}")
    message(STATUS "IDE_FOLDER: ${INTERFACE_IDE_FOLDER}")

    message(STATUS "***create interface : ${INTERFACE_MODULE_NAME}***")
    if (INTERFACE_UNPARSED_ARGUMENTS)
        message(WARNING "Unrecognized arguments: ${INTERFACE_UNPARSED_ARGUMENTS}")
    endif()
    if (NOT DEFINED INTERFACE_MODULE_NAME)
        message(FATAL_ERROR "MODULE_NAME is not defined, please set it.")
    endif()

    add_library(${INTERFACE_MODULE_NAME} INTERFACE)
    target_sources(${INTERFACE_MODULE_NAME}
            INTERFACE FILE_SET HEADERS 
            BASE_DIRS ${INTERFACE_TARGET_SOURCE_HEADER_BASE_DIR}
            FILES ${INTERFACE_TARGET_SOURCE_PUBLIC_HEADER}
        )

    if (INTERFACE_TARGET_DEPENDENCIES_PUBLIC)
        message(STATUS "will add public link to ${INTERFACE_MODULE_NAME}, link librarys: ${INTERFACE_TARGET_DEPENDENCIES_PUBLIC}")
        target_link_libraries(${INTERFACE_MODULE_NAME} INTERFACE ${INTERFACE_TARGET_DEPENDENCIES_PUBLIC})
    endif()

    LinkTargetIncludeDirectories(
        INTERFACE
        MODULE_NAME ${INTERFACE_MODULE_NAME}
        TARGET_INCLUDE_DIRECTORIES_BUILD_INTERFACE ${INTERFACE_TARGET_INCLUDE_DIRECTORIES_BUILD_INTERFACE}
        TARGET_INCLUDE_DIRECTORIES_INSTALL_INTERFACE ${INTERFACE_TARGET_INCLUDE_DIRECTORIES_INSTALL_INTERFACE}
    )
    
    BuildInstallModule(
            MODULE_NAME ${INTERFACE_MODULE_NAME}
    )

    add_custom_target(${INTERFACE_MODULE_NAME}Header SOURCES ${INTERFACE_TARGET_SOURCE_PUBLIC_HEADER})
    if(DEFINED  INTERFACE_IDE_FOLDER)
            set_target_properties(${INTERFACE_MODULE_NAME}Header PROPERTIES FOLDER ${INTERFACE_IDE_FOLDER})
    endif()

    # add_library(${INTERFACE_MODULE_NAME}Interface ALIAS ${INTERFACE_MODULE_NAME})

    message(STATUS "====Finish Build Interface: ${INTERFACE_MODULE_NAME}====")
endfunction()